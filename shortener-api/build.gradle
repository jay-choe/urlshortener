plugins {
	id 'org.springframework.boot' version '2.5.3'
	id 'io.spring.dependency-management' version '1.0.13.RELEASE'
	id 'org.asciidoctor.jvm.convert' version "3.3.2"
	id 'com.epages.restdocs-api-spec' version '0.16.2'
	id 'java'
}

group = 'com.shortener'
version = '0.0.1'
sourceCompatibility = '11'

configurations {
	all*.exclude module : 'spring-boot-starter-logging'
	all*.exclude module : 'logback-classic'

	asciidoctorExtensions

	compileOnly {
		extendsFrom annotationProcessor
	}
}

ext {
	snippetsDir = file("build/generated-snippets")
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springdoc:springdoc-openapi-webmvc-core:1.6.13'

	compileOnly 'org.projectlombok:lombok'
	compileOnly 'com.aventrix.jnanoid:jnanoid:2.0.0'

	runtimeOnly 'mysql:mysql-connector-java'
	runtimeOnly 'com.h2database:h2:2.1.214'

	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.testcontainers:testcontainers:1.17.6'
	testImplementation 'it.ozimov:embedded-redis:0.7.3'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation('com.epages:restdocs-api-spec:0.16.2')
	testImplementation('com.epages:restdocs-api-spec-mockmvc:0.16.2')

	asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

	testCompileOnly 'org.projectlombok:lombok:1.18.12'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'

}

tasks.named("test") {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

tasks.register("restDocsTest", Test) {
	finalizedBy 'openapi3'
	finalizedBy 'asciidoctor'
	finalizedBy 'copyOASToSwagger'
}

tasks.register("copyOASToSwagger", Copy.class) {
	delete("src/main/resources/static/swagger-ui/openapi3.yaml")
	from("build/api-spec/openapi3.yaml")
	into("src/main/resources/static/swagger-ui/.")
	dependsOn "openapi3"
}

tasks.named("asciidoctor") {
	dependsOn restDocsTest

	configurations 'asciidoctorExtensions'
	inputs.dir snippetsDir
	baseDirFollowsSourceDir()
}


openapi3 {
	servers = [ {
		url = 'http://localhost:8080'}
	]
	title = 'short URL docs'
	description('short URL docs')
	version("${project.version}")
	format = 'yaml'
}
